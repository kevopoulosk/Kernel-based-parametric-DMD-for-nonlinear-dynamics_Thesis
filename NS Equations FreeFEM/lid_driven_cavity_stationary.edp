// load "iovtk"
// include "macro_ddm.idp"

real uin = 4; // initial velocity
int m = 75; //number of elements  
real L = 1; // length of the square


string PathToDirectory = "/Users/konstantinoskevopoulos/Documents/SnapshotData/lid_driven_cavity/";

// define and construct the mesh
border C1(t = 0, L){ x = t; y = 0; label = 1; } // noslip (bottom of the mesh)
border C2(t = 0, L){ x = L; y = t; label = 2; } // noslip (right of the mesh)
border C3(t = 0, L){ x = L-t; y = L; label = 3; } // inflow (top of the mesh )
border C4(t = 0, L){ x = 0; y = L-t; label = 4; } // noslip (left of the mesh )
mesh Th = buildmesh( C1(m) + C2(m) + C3(m) + C4(m)); // same number of elements in every edge
plot(Th, wait=1);

cout << "Number of Elements: " + Th.nt << endl;



real mu = 0.01;
real rho = 1;
real nu = mu/rho;

real Re = uin*L*1/nu;

// this has to do with the vtk file
int[int] orderOut = [1, 1, 1, 1];

real SampleSize = 600.; // Size of the array of andom numbers 
real Re20 = 0.2;              // Velocity for Re=20
real Re1200 = 12.;              // Velocity for Re=1200
real[int] velocitiesSample(SampleSize);

// Function to generate random numbers within a given range
func real RNG(real low, real high, int size) {
    return low + (high - low) * randreal1();
}

int i,j;

fespace Vh(Th,P2);
Vh u, v, uu, vv, up, vp;
fespace Ph(Th, P1);
Ph p, pp, q;




macro div(u,v)(dx(u)+dy(v))//
macro grad(u)[dx(u),dy(u)]//


problem NSunst([u,v,p],[uu,vv,pp],solver=sparsesolver)=
int2d(Th)(nu*(grad(u)'*grad(uu)+grad(v)'*grad(vv)))
+int2d(Th)([up,vp]'*grad(u)*uu+[up,vp]'*grad(v)*vv)
-int2d(Th)(p*div(uu,vv)+pp*div(u,v))
+int2d(Th)(1.e-10*p*pp)
+on(1,2,4,u=0,v=0)
+on(3,u=uin,v=0)
;



//for each sample
for (j=0;j<SampleSize;j++){
    for (i=0;i<70;i++){ // here starts the fixed-point iteration for the nonlinearity
        NSunst;
        up[]=u[];
        vp[]=v[];
        pp[]=p[];

}
    
    plot([u,v], wait = 1, cmm="Reynolds="+Re);

    // if (j==0) {
    //     savevtk("lid_driven_cavity_stationary.vtk", Th, u, v, p, dataname=" u v p", order=orderOut);
    // }

    {ofstream fout(PathToDirectory+"/u"+"/sample_"+j+"_u.txt");
    fout << u[] << endl;
    }

    {ofstream fout(PathToDirectory+"/v"+"/sample_"+j+"_v.txt");
    fout << v[] << endl;
    }

    {ofstream fout(PathToDirectory+"/p"+"/sample_"+j+"_p.txt");
    fout << p[] << endl;
    }

    cout << "Sample No."+ j+ " saved" << endl;


    real randomnum = RNG(Re20, Re1200, SampleSize);
    velocitiesSample(j) = randomnum;
    uin = randomnum;
    Re = randomnum*L*1/nu;

}


{ofstream fout(PathToDirectory+"/velocities"+"/velocities_sampled.txt");
  fout << velocitiesSample << endl;
}
cout << "Array of randomly sampled velocities is saved." << endl;
